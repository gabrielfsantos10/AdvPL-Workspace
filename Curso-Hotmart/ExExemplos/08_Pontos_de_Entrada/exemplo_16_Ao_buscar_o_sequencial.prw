//Bibliotecas
#Include "Totvs.ch"
#Include "TopConn.ch"
 
/*/{Protheus.doc} CriaSXE
Ponto de Entrada para controlar a numeração
@author Atilio
@since 17/10/2019
@version 1.0
@return cCodRet, Código de retorno que entrará no lugar do chamado pelo License
@type function
@see https://tdn.totvs.com/pages/releaseview.action?pageId=6815179
@obs Esse p.e. é chamado na primeira vez de pegar a numeração na tabela
/*/
 
User Function CriaSXE()
    Local aArea      := GetArea()
    Local cAliasX    := ParamIXB[1] //cAlias - Nome da tabela;
    Local cCpoSX8X   := ParamIXB[2] //cCpoSX8 - Nome do campo que será utilizado para verificar o próximo sequencial;
    Local cCodRet    := Nil
    Local cQryMax    := ""
    Local cUltimo    := ""
     
    //Se for a tabela SU7 (Operadores - TMKA090)
    If cAliasX == "SU7"
         
        //Montando a query, pegando o último
        cUltimo := PadR("O", TamSX3('U7_COD')[1], "0")
        cQryMax := " SELECT " + CRLF
        cQryMax += "     ISNULL(MAX(" + cCpoSX8X + "), '" + cUltimo + "') AS ULTIMO " + CRLF 
        cQryMax += " FROM  " + CRLF
        cQryMax += "     " + RetSQLName('SU7') + " SU7 " + CRLF
        cQryMax += " WHERE  " + CRLF
        cQryMax += "     U7_FILIAL = '" + FWxFilial('SU7') + "' " + CRLF
        cQryMax += "     AND LEFT(U7_COD, 1) = 'O' " + CRLF
        cQryMax += "     AND SU7.D_E_L_E_T_ = ' ' " + CRLF
        TCQuery cQryMax New Alias "QRY_MAX"
         
        //Se tiver dados e se não tiver vazio
        If ! QRY_MAX->(EoF()) .And. ! Empty(QRY_MAX->ULTIMO)
            cCodRet := Soma1(QRY_MAX->ULTIMO)
        EndIf
        QRY_MAX->(DbCloseArea())
         
    EndIf
     
    RestArea(aArea)
Return cCodRet
